
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ЗанятостьКлассов.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.Расписание");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(ДатаНачала, ДатаОкончания));
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗанятостьКлассов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(ДатаНачала, ДатаОкончания));
	
	Блокировка.Заблокировать();
	
	Движения.ЗанятостьКлассов.Очистить();
	Движения.ЗанятостьКлассов.Записать();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Расписание.Класс КАК Класс,
		|	Расписание.Дата КАК Дата,
		|	Расписание.Класс.КоличествоМест КАК МестВсего
		|ПОМЕСТИТЬ втРасписание
		|ИЗ
		|	РегистрСведений.Расписание КАК Расписание
		|ГДЕ
		|	Расписание.Курс = &Курс
		|	И Расписание.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Класс,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРасписание.Класс КАК Класс,
		|	втРасписание.Дата КАК Дата,
		|	втРасписание.МестВсего - ЕСТЬNULL(ЗанятостьКлассовОбороты.ЗанятоМестОборот, 0) КАК СвободноМест
		|ИЗ
		|	втРасписание КАК втРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятостьКлассов.Обороты(
		|				,
		|				,
		|				,
		|				Класс В
		|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|							втРасписание.Класс
		|						ИЗ
		|							втРасписание)
		|					И (Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)) КАК ЗанятостьКлассовОбороты
		|		ПО втРасписание.Класс = ЗанятостьКлассовОбороты.Класс
		|			И втРасписание.Дата = ЗанятостьКлассовОбороты.Дата
		|ГДЕ
		|	втРасписание.МестВсего - ЕСТЬNULL(ЗанятостьКлассовОбороты.ЗанятоМестОборот, 0) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	втРасписание.Класс.КоличествоМест УБЫВ
		|ИТОГИ
		|	СУММА(СвободноМест)
		|ПО
		|	Дата");
	
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	ВыборкаДата = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДата.Следующий() Цикл
		Если ВыборкаДата.СвободноМест = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = ВыборкаДата.Выбрать();
		Если Выборка.Следующий() Тогда
			Движение = Движения.ЗанятостьКлассов.Добавить();
			Движение.Период		 = Дата;
			Движение.Класс		 = Выборка.Класс;
			Движение.Дата		 = Выборка.Дата;
			Движение.ЗанятоМест	 = 1;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
