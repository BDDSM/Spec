
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	НаборОсновныеНачисления = Движения.ОсновныеНачисления;
	НаборДополнительныеНачисления = Движения.ДополнительныеНачисления;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОсновныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисления.Подразделение КАК Подразделение,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	КОНЕЦПЕРИОДА(ОсновныеНачисления.ПериодДействияКонец, ДЕНЬ) КАК ПериодДействияКонец,
		|	ВЫБОР ОсновныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.Больничный)
		|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ОсновныеНачисления.ПериодДействияНачало, МЕСЯЦ), МЕСЯЦ, -1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР ОсновныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.Больничный)
		|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ОсновныеНачисления.ПериодДействияКонец, МЕСЯЦ), МЕСЯЦ, -1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодКонец,
		|	ОсновныеНачисления.Размер КАК Размер,
		|	ОсновныеНачисления.График КАК График
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборОсновныеНачисления);
	
	ТаблицаСторноЗаписей = НаборОсновныеНачисления.ПолучитьДополнение();
	Для Каждого СтрокаСторно Из ТаблицаСторноЗаписей Цикл
		ДобавитьСтрокуСторно(НаборОсновныеНачисления, СтрокаСторно);
	КонецЦикла;
	
	НаборОсновныеНачисления.Записать(, Ложь);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисления.Подразделение КАК Подразделение,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР ДополнительныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Премия)
		|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ), МЕСЯЦ, -1)
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ)
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР ДополнительныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Премия)
		|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ), МЕСЯЦ, -1)
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ)
		|	КОНЕЦ КАК БазовыйПериодКонец
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборДополнительныеНачисления);
	НаборДополнительныеНачисления.Записать(, Ложь);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Период", ПериодРегистрации);
	Запрос.УстановитьПараметр("НачалоПериодаРегистрации", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Компенсация", Константы.КомпенсацияЗаЛекарства.Получить());
	
	РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос);
	РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос);
КонецПроцедуры

Процедура ДобавитьСтрокуСторно(НаборЗаписей, СтрокаСторно)
	ЗаписьРегистра = НаборЗаписей.Добавить();
	
	ЗаполнитьЗначенияСвойств(ЗаписьРегистра, СтрокаСторно);
	
	ЗаписьРегистра.ПериодРегистрации    = СтрокаСторно.ПериодРегистрацииСторно;
	ЗаписьРегистра.ПериодДействияНачало = СтрокаСторно.ПериодДействияНачалоСторно;
	ЗаписьРегистра.ПериодДействияКонец  = СтрокаСторно.ПериодДействияКонецСторно;
	ЗаписьРегистра.Сторно               = Истина;
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(ДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ФактЧасов,
		|	ЕСТЬNULL(ДанныеГрафика.ДнейБазовыйПериод, 0) КАК БазаДней,
		|	ЕСТЬNULL(ДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ФактДней,
		|	ЕСТЬNULL(ДанныеГрафика.ДнейПериодДействия, 0) КАК НормаДней,
		|	ЕСТЬNULL(ДанныеГрафика.ЧасовПериодРегистрации, 0) КАК НормаЧасов,
		|	ЕСТЬNULL(ДанныеГрафика.ВечерниеФактическийПериодДействия, 0) КАК Вечерние,
		|	ЕСТЬNULL(БазаОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Тариф, 0) КАК Тариф
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ДанныеГрафика
		|		ПО ОсновныеНачисления.НомерСтроки = ДанныеГрафика.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаОсновныеНачисления
		|		ПО ОсновныеНачисления.НомерСтроки = БазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(&Период, ) КАК СведенияОСотрудникахСрезПоследних
		|		ПО ОсновныеНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборОсновныеНачисления, Выборка);
	НаборОсновныеНачисления.Записать(, Истина);
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(БазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(БазаДополнительныеНачисления.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(БазаОсновныеНачисления.ДниБолезниБаза, 0) КАК ДниБолезни,
		|	&Компенсация КАК Компенсация,
		|	ЕСТЬNULL(ПроцентыПремииСрезПоследних.Значение, 0) КАК ПроцентПремии
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаОсновныеНачисления
		|		ПО ДополнительныеНачисления.НомерСтроки = БазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаДополнительныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаДополнительныеНачисления
		|		ПО ДополнительныеНачисления.НомерСтроки = БазаДополнительныеНачисления.НомерСтроки,
		|	РегистрСведений.ПроцентыПремии.СрезПоследних(&НачалоПериодаРегистрации, ) КАК ПроцентыПремииСрезПоследних
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборДополнительныеНачисления, Выборка);
	НаборДополнительныеНачисления.Записать(, Истина);
КонецПроцедуры
