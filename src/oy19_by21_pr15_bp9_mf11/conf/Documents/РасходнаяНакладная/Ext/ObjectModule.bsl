
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	НоменклатураПустаяСсылка = Справочники.Номенклатура.ПустаяСсылка();
	
	Движения.Продажи.Очистить();
	Движения.ОстаткиНоменклатуры.Очистить();
	
	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(Товары.Номенклатура.Вид <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)) КАК ЭтоТовар,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	взГруппыАналогов.ГруппаАналогов КАК ГруппаАналогов,
		|	взГруппыАналогов.Номенклатура КАК Номенклатура,
		|	АналогиТоваровСрезПоследних.Номенклатура КАК Аналог,
		|	НоменклатураАналога.Артикул КАК АртикулАналога
		|ПОМЕСТИТЬ втАналоги
		|ИЗ
		|	(ВЫБРАТЬ
		|		АналогиТоваровСрезПоследних.Номенклатура КАК Номенклатура,
		|		АналогиТоваровСрезПоследних.ГруппаАналогов КАК ГруппаАналогов
		|	ИЗ
		|		РегистрСведений.АналогиТоваров.СрезПоследних(
		|				&Период,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ.Номенклатура
		|					ИЗ
		|						втТЧ КАК втТЧ
		|					ГДЕ
		|						втТЧ.ЭтоТовар)) КАК АналогиТоваровСрезПоследних) КАК взГруппыАналогов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналогиТоваров.СрезПоследних(&Период, ) КАК АналогиТоваровСрезПоследних
		|		ПО взГруппыАналогов.ГруппаАналогов = АналогиТоваровСрезПоследних.ГруппаАналогов
		|			И взГруппыАналогов.Номенклатура <> АналогиТоваровСрезПоследних.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураАналога
		|		ПО (АналогиТоваровСрезПоследних.Номенклатура = НоменклатураАналога.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ втТоварыИАналоги
		|ИЗ
		|	втТЧ КАК втТЧ
		|ГДЕ
		|	втТЧ.ЭтоТовар
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	втАналоги.Аналог
		|ИЗ
		|	втАналоги КАК втАналоги
		|
		|СГРУППИРОВАТЬ ПО
		|	втАналоги.Аналог
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТоварыИАналоги.Номенклатура КАК Номенклатура
		|ИЗ
		|	втТоварыИАналоги КАК втТоварыИАналоги"
	);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	Блокировка.Заблокировать();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.ЭтоТовар КАК ЭтоТовар,
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма,
		|	ЕСТЬNULL(втАналоги.Аналог, НЕОПРЕДЕЛЕНО) КАК Аналог
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ втАналоги КАК втАналоги
		|		ПО втТЧ.Номенклатура = втАналоги.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	втАналоги.АртикулАналога
		|ИТОГИ
		|	МАКСИМУМ(ЭтоТовар),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма)
		|ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Остаток,
		|	ОстаткиНоменклатурыОстатки.СтоимостьОстаток КАК Стоимость
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						втТоварыИАналоги.Номенклатура
		|					ИЗ
		|						втТоварыИАналоги КАК втТоварыИАналоги)
		|				И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	РезультатПакет = Запрос.ВыполнитьПакет();
	ИндексПоследнегоВПакете = РезультатПакет.Количество() - 1;
	
	ОстаткиТоваровИАналогов = Новый Соответствие;
	
	Выборка = РезультатПакет[ИндексПоследнегоВПакете].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОстаткиТоваровИАналогов.Вставить(Выборка.Номенклатура, Новый Структура("Остаток, Стоимость", Выборка.Остаток, Выборка.Стоимость));
	КонецЦикла;
	
	ВыборкаНоменклатура = РезультатПакет[ИндексПоследнегоВПакете-1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если НЕ ВыборкаНоменклатура.ЭтоТовар Тогда
			ДобавитьДвижениеПродажи(ВыборкаНоменклатура.Номенклатура, НоменклатураПустаяСсылка, ВыборкаНоменклатура.Количество, ВыборкаНоменклатура.Сумма, 0);
			
			Продолжить;
		КонецЕсли;
		
		ОстаткиТовара = ОстаткиТоваровИАналогов.Получить(ВыборкаНоменклатура.Номенклатура);
		Если ОстаткиТовара = Неопределено Тогда
			ОстаткиТовара = Новый Структура("Остаток, Стоимость", 0, 0);
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		Если ОсталосьСписать <= ОстаткиТовара.Остаток Тогда
			Себестоимость = ?(ОсталосьСписать = ОстаткиТовара.Остаток, ОстаткиТовара.Стоимость, ОсталосьСписать / ОстаткиТовара.Остаток * ОстаткиТовара.Стоимость);
			
			ДобавитьДвижениеОстатки(ВыборкаНоменклатура.Номенклатура, ОсталосьСписать, Себестоимость);
			ДобавитьДвижениеПродажи(ВыборкаНоменклатура.Номенклатура, НоменклатураПустаяСсылка, ВыборкаНоменклатура.Количество, ВыборкаНоменклатура.Сумма, Себестоимость);
			
			ОстаткиТовара.Остаток   = ОстаткиТовара.Остаток - ОсталосьСписать;
			ОстаткиТовара.Стоимость = ОстаткиТовара.Стоимость - Себестоимость;
		Иначе
			Если ОстаткиТовара.Остаток > 0 Тогда
				Себестоимость = ОстаткиТовара.Стоимость;
				СуммаПродажи = ОстаткиТовара.Остаток / ВыборкаНоменклатура.Количество * ВыборкаНоменклатура.Сумма;
				
				ДобавитьДвижениеОстатки(ВыборкаНоменклатура.Номенклатура, ОстаткиТовара.Остаток, Себестоимость);
				ДобавитьДвижениеПродажи(ВыборкаНоменклатура.Номенклатура, НоменклатураПустаяСсылка, ОстаткиТовара.Остаток, СуммаПродажи, Себестоимость);
				
				ОсталосьСписать = ОсталосьСписать - ОстаткиТовара.Остаток;
				
				ОстаткиТовара.Остаток   = 0;
				ОстаткиТовара.Стоимость = 0;
			КонецЕсли;
			
			Выборка = ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если НЕ ЗначениеЗаполнено(Выборка.Аналог) Тогда
					Продолжить;
				КонецЕсли;
				
				ОстаткиТовара = ОстаткиТоваровИАналогов.Получить(Выборка.Аналог);
				Если ОстаткиТовара = Неопределено ИЛИ ОстаткиТовара.Остаток = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Списать = Мин(ОсталосьСписать, ОстаткиТовара.Остаток);
				Себестоимость = ?(Списать = ОстаткиТовара.Остаток, ОстаткиТовара.Стоимость, Списать / ОстаткиТовара.Остаток * ОстаткиТовара.Стоимость);
				СуммаПродажи = Списать / Выборка.Количество * Выборка.Сумма;
				
				ДобавитьДвижениеОстатки(Выборка.Аналог, Списать, Себестоимость);
				ДобавитьДвижениеПродажи(Выборка.Номенклатура, Выборка.Аналог, Списать, СуммаПродажи, Себестоимость);
				
				ОстаткиТовара.Остаток   = ОстаткиТовара.Остаток - Списать;
				ОстаткиТовара.Стоимость = ОстаткиТовара.Стоимость - Себестоимость;
				
				ОсталосьСписать = ОсталосьСписать - Списать;
				Если ОсталосьСписать = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ОсталосьСписать > 0 Тогда
				Отказ = Истина;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Не хватает товара %1 и его аналогов в количестве %2 штук", ВыборкаНоменклатура.Номенклатура, ОсталосьСписать - ОстаткиТовара.Остаток);
				Сообщение.Сообщить();
				
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДвижениеОстатки(Номенклатура, Количество, Стоимость)
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период       = Дата;
	Движение.Номенклатура = Номенклатура;
	Движение.Склад        = Склад;
	Движение.Количество   = Количество;
	Движение.Стоимость    = Стоимость;
КонецПроцедуры

Процедура ДобавитьДвижениеПродажи(Номенклатура, Аналог, Количество, Сумма, Себестоимость)
	Движение = Движения.Продажи.Добавить();
	Движение.Период        = Дата;
	Движение.Номенклатура  = Номенклатура;
	Движение.Аналог        = Аналог;
	Движение.Количество    = Количество;
	Движение.Сумма         = Сумма;
	Движение.Себестоимость = Себестоимость;
КонецПроцедуры
