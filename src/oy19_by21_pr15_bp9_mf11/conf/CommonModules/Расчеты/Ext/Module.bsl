
Процедура ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборЗаписей) Экспорт
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьЗаписиРегистраРасчета(НаборЗаписей, Выборка) Экспорт
	ВидРасчетаОплатаПоТарифу    = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу;
	ВидРасчетаБольничный        = ПланыВидовРасчета.ОсновныеНачисления.Больничный;
	ВидРасчетаКомпенсацияЗатрат = ПланыВидовРасчета.ДополнительныеНачисления.КомпенсацияЗатрат;
	ВидРасчетаПремия            = ПланыВидовРасчета.ДополнительныеНачисления.Премия;
	
	Пока Выборка.Следующий() Цикл
		Запись = НаборЗаписей[Выборка.НомерСтроки-1];
		
		Результат = 0;
		
		Если Выборка.ВидРасчета = ВидРасчетаОплатаПоТарифу Тогда
			Результат = Выборка.ФактЧасов * Выборка.Тариф + Выборка.Вечерние * Выборка.Тариф * 0.5;
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаБольничный Тогда
			Запись.ДниБолезни = Выборка.ФактДней;
			Результат = ?(Выборка.БазаДней = 0, 0, Выборка.РезультатБаза / Выборка.БазаДней * Выборка.ФактДней / 2);
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаКомпенсацияЗатрат Тогда
			Результат = Выборка.ДниБолезни * Выборка.Компенсация;
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаПремия Тогда
			Результат = Выборка.База * Выборка.ПроцентПремии / 100;
		КонецЕсли;
		
		Запись.Результат = Результат;
	КонецЦикла;
КонецПроцедуры
