
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина; // работает для включённого у РН флага "Разрешить разделение итогов"
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТовары.Количество * РасходнаяНакладнаяТовары.Коэффициент) КАК Количество,
		|	&Склад КАК Склад,
		|	&Период КАК Период,
		|	&ВидДвижения КАК ВидДвижения
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Склад КАК Склад,
		|	втТЧ.Период КАК Период,
		|	втТЧ.ВидДвижения КАК ВидДвижения
		|ИЗ
		|	втТЧ КАК втТЧ");
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиНоменклатуры.Загрузить(РезультатЗапроса.Выгрузить());
	Движения.Записать(); // сбрасывает флаг "Записывать" у коллекций движений с взведённым этим флагом в отличие от Движения.ОстаткиНоменклатуры.Записать();
	
	МоментВремени = ?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК НеХватает
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ
		|					втТЧ.Номенклатура КАК Номенклатура,
		|					втТЧ.Склад КАК Склад
		|				ИЗ
		|					втТЧ КАК втТЧ)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	РезультатПоОстаткам = Запрос.Выполнить();
	Если НЕ РезультатПоОстаткам.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатПоОстаткам.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара: %1 в количестве %2", Выборка.НоменклатураПредставление, Выборка.НеХватает);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.СтоимостьТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	БлокировкаДанных.Заблокировать();
	
	Движения.СтоимостьТоваров.Очистить(); // если на форме выведены движения, тогда при перепроведении коллекция движений будет заполнена – принудительно очищаем
	Движения.СтоимостьТоваров.Записать();
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Период КАК Период,
		|	втТЧ.ВидДвижения КАК ВидДвижения,
		|	ВЫБОР ЕСТЬNULL(СтоимостьТоваровОстатки.КоличествоОстаток, 0)
		|		КОГДА 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР втТЧ.Количество
		|				КОГДА СтоимостьТоваровОстатки.КоличествоОстаток
		|					ТОГДА СтоимостьТоваровОстатки.СтоимостьОстаток
		|				ИНАЧЕ СтоимостьТоваровОстатки.СтоимостьОстаток / СтоимостьТоваровОстатки.КоличествоОстаток * втТЧ.Количество
		|			КОНЕЦ
		|	КОНЕЦ КАК Стоимость
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ.Номенклатура
		|					ИЗ
		|						втТЧ КАК втТЧ)) КАК СтоимостьТоваровОстатки
		|		ПО втТЧ.Номенклатура = СтоимостьТоваровОстатки.Номенклатура";
	
	Если Режим = РежимПроведенияДокумента.Неоперативный Тогда
		МоментВремени = МоментВремени();
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	КонецЕсли;
	
	Движения.СтоимостьТоваров.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры
