
&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	НастройкиПользователя = Список.КомпоновщикНастроек.ПолучитьНастройки();
	
	ИерархияГруппировок = Неопределено;
	ПолучитьИерархиюГруппировок(НастройкиПользователя.Структура, ИерархияГруппировок);
	
	Настройки.Вставить("Группировки", ИерархияГруппировок);
	Настройки.Вставить("Сортировка", НастройкиПользователя.Порядок); 
	Настройки.Вставить("Отбор", НастройкиПользователя.Отбор);
КонецПроцедуры

&НаСервере
Процедура ПолучитьИерархиюГруппировок(СтруктураНастроек, УровеньГруппировок)
	Если СтруктураНастроек.Количество() > 0 Тогда
		УровеньГруппировок = Новый Структура("ПоляГруппировки, Следующий");
		
		ПолучитьИерархиюГруппировок(СтруктураНастроек[0].Структура, УровеньГруппировок.Следующий);
		
		УровеньГруппировок.ПоляГруппировки = СтруктураНастроек[0].ПоляГруппировки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИерархиюГруппировок(СтруктураНастроек, УровеньГруппировки)
	Если УровеньГруппировки <> Неопределено Тогда
		ГруппировкаКомпоновкиДанных = СтруктураНастроек.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		
		Для Каждого Элемент Из УровеньГруппировки.ПоляГруппировки.Элементы Цикл
			НовыйЭлемент = ГруппировкаКомпоновкиДанных.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элемент);
		КонецЦикла;
		
		ЗаполнитьИерархиюГруппировок(ГруппировкаКомпоновкиДанных.Структура, УровеньГруппировки.Следующий);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	НастройкиПользователя = Список.КомпоновщикНастроек.ПолучитьНастройки();
	НастройкиПользователя.Отбор.Элементы.Очистить();
	НастройкиПользователя.Порядок.Элементы.Очистить();
	НастройкиПользователя.Структура.Очистить();
	
	нсОтбор = Настройки.Получить("Отбор");
	Если нсОтбор <> Неопределено Тогда
		Для Каждого Элемент Из нсОтбор.Элементы Цикл
			НовыйЭлемент = НастройкиПользователя.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элемент);
		КонецЦикла;
	КонецЕсли;
	
	нсПорядок = Настройки.Получить("Сортировка");
	Если нсПорядок <> Неопределено Тогда
		Для Каждого Элемент Из нсПорядок.Элементы Цикл
			НовыйЭлемент = НастройкиПользователя.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элемент);
		КонецЦикла;
	КонецЕсли;
	
	ИерархияГруппировок = Настройки.Получить("Группировки");
	ЗаполнитьИерархиюГруппировок(НастройкиПользователя.Структура, ИерархияГруппировок);
	
	Список.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиПользователя);
КонецПроцедуры
