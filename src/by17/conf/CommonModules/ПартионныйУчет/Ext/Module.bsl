
Функция ТаблицаСписанияПартий(ДокументОбъект, Склад, МенеджерВременныхТаблиц, Отказ) Экспорт
	ТаблицаПартий = Новый ТаблицаЗначений;
	ТаблицаПартий.Колонки.Добавить("Номенклатура");
	ТаблицаПартий.Колонки.Добавить("Партия");
	ТаблицаПартий.Колонки.Добавить("Количество");
	ТаблицаПартий.Колонки.Добавить("Сумма");
	ТаблицаПартий.Колонки.Добавить("Выручка");
	
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(ДокументОбъект.Дата);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана учетная политика на " + НачалоГода(ДокументОбъект.Дата);
		Сообщение.Сообщить();
		
		Возврат ТаблицаПартий;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура
		|ИЗ
		|	втТЧ КАК втТЧ");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Проводки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, ДокументОбъект.Дата));
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	ДокументОбъект.Движения.Проводки.Очистить();
	ДокументОбъект.Движения.Проводки.Записать();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.НоменклатураПредставление КАК НоменклатураПредставление,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма,
		|	ПроводкиОстатки.Субконто3 КАК Партия,
		|	ПроводкиОстатки.СуммаОстатокДт КАК СуммаОстаток,
		|	ЕСТЬNULL(ПроводкиОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(
		|				&МоментВремени,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.Товары),
		|				,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							втТЧ.Номенклатура
		|						ИЗ
		|							втТЧ)
		|					И Субконто2 = &Склад) КАК ПроводкиОстатки
		|		ПО втТЧ.Номенклатура = ПроводкиОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПроводкиОстатки.Субконто3.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(НоменклатураПредставление),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МоментВремени", ДокументОбъект.МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Если УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Нехватка = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Нехватка > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1 в количестве %2 штук", ВыборкаНоменклатура.НоменклатураПредставление, Нехватка);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		СуммаВыручки = ВыборкаНоменклатура.Сумма;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
			Если Выборка.КоличествоОстаток <= 0  Тогда
				Продолжить;
			КонецЕсли;
			
			Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			СуммаПартии = ?(ОсталосьСписать = Выборка.КоличествоОстаток, Выборка.СуммаОстаток, Списать * Выборка.СуммаОстаток / Выборка.КоличествоОстаток);
			
			СтрПартий = ТаблицаПартий.Добавить();
			СтрПартий.Номенклатура	 = Выборка.Номенклатура;
			СтрПартий.Партия		 = Выборка.Партия;
			СтрПартий.Количество	 = Списать;
			СтрПартий.Сумма			 = СуммаПартии;
			СтрПартий.Выручка		 = СуммаВыручки;
			
			СуммаВыручки = 0;
			ОсталосьСписать = ОсталосьСписать - Списать;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаПартий;
КонецФункции