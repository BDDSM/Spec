
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана учетная политика на " + НачалоГода(Дата);
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	Движения.Проводки.Записывать = Истина;
	Движения.Проводки.Очистить();
	Движения.Проводки.Записать();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.НоменклатураПредставление КАК НоменклатураПредставление,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма,
		|	ПроводкиОстатки.Субконто3 КАК Партия,
		|	ПроводкиОстатки.СуммаОстатокДт КАК СуммаОстаток,
		|	ЕСТЬNULL(ПроводкиОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(
		|				&МоментВремени,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.Товары),
		|				,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							втТЧ.Номенклатура
		|						ИЗ
		|							втТЧ)
		|					И Субконто2 = &Склад) КАК ПроводкиОстатки
		|		ПО втТЧ.Номенклатура = ПроводкиОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПроводкиОстатки.Субконто3.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(НоменклатураПредставление),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура");
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Если УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Нехватка = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Нехватка > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1 в количестве %2 штук", ВыборкаНоменклатура.НоменклатураПредставление, Нехватка);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
			Если Выборка.КоличествоОстаток <= 0  Тогда
				Продолжить;
			КонецЕсли;
			
			Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			СуммаПартии = ?(ОсталосьСписать = Выборка.КоличествоОстаток, Выборка.СуммаОстаток, Списать * Выборка.СуммаОстаток / Выборка.КоличествоОстаток);
			
			Проводка = Движения.Проводки.Добавить();
			Проводка.Период			 = Дата;
			Проводка.СчетДт			 = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт			 = ПланыСчетов.Управленческий.Товары;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]	 = Выборка.Номенклатура;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады]		 = Склад;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]	 = Выборка.Номенклатура;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады]		 = Склад;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии]		 = Выборка.Партия;
			Проводка.КоличествоКт	 = Списать;
			Проводка.Сумма			 = СуммаПартии;
			
			ОсталосьСписать = ОсталосьСписать - Списать;
		КонецЦикла;
		
		Проводка = Движения.Проводки.Добавить();
		Проводка.Период			 = Дата;
		Проводка.СчетДт			 = ПланыСчетов.Управленческий.Покупатели;
		Проводка.СчетКт			 = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]	 = Выборка.Номенклатура;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады]		 = Склад;
		Проводка.Сумма			 = ВыборкаНоменклатура.Сумма;
	КонецЦикла;
КонецПроцедуры
