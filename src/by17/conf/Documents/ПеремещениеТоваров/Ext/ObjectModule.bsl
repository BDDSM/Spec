
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.Проводки.Записывать = Истина;
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеТоваровТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество,
		|	0 КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПеремещениеТоваровТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура");
	
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Выполнить();
	
	ТаблицаПартий = ПартионныйУчет.ТаблицаСписанияПартий(ЭтотОбъект, СкладОтправитель, МВТ, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрПартий Из ТаблицаПартий Цикл
		Проводка = Движения.Проводки.Добавить();
		Проводка.Период			 = Дата;
		Проводка.СчетДт			 = ПланыСчетов.Управленческий.Товары;
		Проводка.СчетКт			 = ПланыСчетов.Управленческий.Товары;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]	 = СтрПартий.Номенклатура;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады]		 = СкладПолучатель;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии]		 = Ссылка;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]	 = СтрПартий.Номенклатура;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады]		 = СкладОтправитель;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии]		 = СтрПартий.Партия;
		Проводка.КоличествоДт	 = СтрПартий.Количество;
		Проводка.КоличествоКт	 = СтрПартий.Количество;
		Проводка.Сумма			 = СтрПартий.Сумма;
	КонецЦикла;
КонецПроцедуры
