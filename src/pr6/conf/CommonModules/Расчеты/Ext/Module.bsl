
Процедура ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборЗаписей) Экспорт
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда
			КоличествоМесяцев = Выборка.КоличествоМесяцев;
			Для Месяц = 0 По КоличествоМесяцев Цикл
				Движение = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				
				НачалоМесяца = ДобавитьМесяц(НачалоМесяца(Выборка.ПериодДействияНачало), Месяц);
				Движение.ПериодДействияНачало = Макс(Выборка.ПериодДействияНачало, НачалоМесяца);
				Движение.ПериодДействияКонец = Мин(Выборка.ПериодДействияКонец, КонецМесяца(НачалоМесяца));
			КонецЦикла;
		Иначе
			Движение = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьЗаписиРегистраРасчета(НаборЗаписей, Выборка) Экспорт
	ВидРасчетаОклад = ПланыВидовРасчета.ОсновныеНачисления.Оклад;
	ВидРасчетаКомандировка = ПланыВидовРасчета.ОсновныеНачисления.Командировка;
	ВидРасчетаПремия = ПланыВидовРасчета.ДополнительныеНачисления.Премия;
	
	Пока Выборка.Следующий() Цикл
		Запись = НаборЗаписей[Выборка.НомерСтроки-1];
		
		Результат = 0;
		
		Если Выборка.ВидРасчета = ВидРасчетаОклад Тогда
			Результат = Выборка.Оклад * Выборка.Факт;
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаПремия Тогда
			Результат = Выборка.РезультатБаза * Запись.Размер / 100;
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаКомандировка Тогда
			Результат = Выборка.Факт * Выборка.РезультатБаза / Выборка.ПланПредыдущегоПериода;
		КонецЕсли;
		
		Запись.Результат = Результат * ?(Запись.Сторно, -1, 1);
	КонецЦикла;
КонецПроцедуры
