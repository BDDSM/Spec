
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не задана учетная политика на %1", НачалоГода(Дата));
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	РасходнаяНакладнаяТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура,
		|	РасходнаяНакладнаяТовары.Номенклатура.Представление,
		|	РасходнаяНакладнаяТовары.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура
		|ИЗ
		|	втТЧ КАК втТЧ");
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	Блокировка.Заблокировать();
	
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.Записать();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.НоменклатураПредставление КАК НоменклатураПредставление,
		|	втТЧ.ВидНоменклатуры КАК ВидНоменклатуры,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма,
		|	Остатки.Партия КАК Партия,
		|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(Остатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Склад = &Склад
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧ.Номенклатура
		|						ИЗ
		|							втТЧ КАК втТЧ)) КАК Остатки
		|		ПО втТЧ.Номенклатура = Остатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Остатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(НоменклатураПредставление),
		|	МАКСИМУМ(ВидНоменклатуры),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Если УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// регистр накопления Продажи
		Движение = Движения.Продажи.Добавить();
		Движение.Период			 = Дата;
		Движение.Номенклатура	 = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество		 = ВыборкаНоменклатура.Количество;
		Движение.Стоимость		 = ВыборкаНоменклатура.Сумма;
		
		// регистр накопления Остатки
		Если ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			Продолжить;
		КонецЕсли;
		
		Нехватка = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Нехватка > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1 на складе %2 в количестве %3 штук", ВыборкаНоменклатура.НоменклатураПредставление, Склад, Нехватка);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() Цикл
			Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			Себестоимость = ?(Списать = Выборка.КоличествоОстаток, Выборка.СтоимостьОстаток, Списать / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток);
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период			 = Дата;
			Движение.Номенклатура	 = Выборка.Номенклатура;
			Движение.Склад			 = Склад;
			Движение.Партия			 = Выборка.Партия;
			Движение.Количество		 = Списать;
			Движение.Стоимость		 = Себестоимость;
			
			ОсталосьСписать = ОсталосьСписать - Списать;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
