
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПрефиксДополнительногоРеквизита = "ДР_";
	
	ОтобразитьДополнительныеРеквизитыМенеджера();
КонецПроцедуры

&НаКлиенте
Процедура МенеджерПриИзменении(Элемент)
	ОтобразитьДополнительныеРеквизитыМенеджера();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.ЗначенияСвойств.Очистить();
	
	ДополнительныеСвойства = ДополнительныеСвойстваМенелжера(Объект.Менеджер);
	Для Каждого Элемент Из ДополнительныеСвойства Цикл
		Значение = ЭтаФорма[ПрефиксДополнительногоРеквизита + Элемент.Имя];
		
		НовоеЗначениеСвойства = Объект.ЗначенияСвойств.Добавить();
		НовоеЗначениеСвойства.Свойство = Элемент.Свойство;
		НовоеЗначениеСвойства.Значение = Значение;
	КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ОтобразитьДополнительныеРеквизитыМенеджера()
	ДобавляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты	 = Новый Массив;
	
	// Получаем удаляемые реквизиты
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого Реквизит Из РеквизитыФормы Цикл
		Если СтрНайти(Реквизит.Имя, ПрефиксДополнительногоРеквизита) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		УдаляемыеРеквизиты.Добавить(Реквизит.Имя);
		
		ЭлементФормы = Элементы.Найти(Реквизит.Имя);
		Если ЭлементФормы <> Неопределено Тогда
			Элементы.Удалить(ЭлементФормы);
		КонецЕсли;
	КонецЦикла;
	
	// Получаем добавляемые реквизиты
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СотрудникиДополнительныеРеквизиты.Реквизит КАК Свойство,
		|	СотрудникиДополнительныеРеквизиты.Реквизит.Код КАК Имя,
		|	СотрудникиДополнительныеРеквизиты.Реквизит.Наименование КАК Заголовок,
		|	СотрудникиДополнительныеРеквизиты.Реквизит.ТипЗначения КАК ТипЗначения
		|ИЗ
		|	Справочник.Сотрудники.ДополнительныеРеквизиты КАК СотрудникиДополнительныеРеквизиты
		|ГДЕ
		|	СотрудникиДополнительныеРеквизиты.Ссылка = &Менеджер");
	
	Запрос.УстановитьПараметр("Менеджер", Объект.Менеджер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НовыйРеквизит = Новый РеквизитФормы(ПрефиксДополнительногоРеквизита + Выборка.Имя, Выборка.ТипЗначения, , Выборка.Заголовок);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для Каждого Реквизит Из ДобавляемыеРеквизиты Цикл
		НовыйЭлемент = Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), ЭтаФорма);
		НовыйЭлемент.Вид		 = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = Реквизит.Имя;
	КонецЦикла;
	
	// Заполняем добавленные реквизиты значениями
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КонтрагентыЗначенияСвойств.Свойство.Код КАК Код,
		|	КонтрагентыЗначенияСвойств.Значение КАК Значение
		|ИЗ
		|	Справочник.Контрагенты.ЗначенияСвойств КАК КонтрагентыЗначенияСвойств
		|ГДЕ
		|	КонтрагентыЗначенияСвойств.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИмяРеквизита = ПрефиксДополнительногоРеквизита + Выборка.Код;
		Если Элементы.Найти(ИмяРеквизита) <> Неопределено Тогда
			ЭтаФорма[ИмяРеквизита] = Выборка.Значение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеСвойстваМенелжера(Менеджер)
	фРезультат = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СотрудникиДополнительныеРеквизиты.Реквизит КАК Свойство,
		|	СотрудникиДополнительныеРеквизиты.Реквизит.Код КАК Имя
		|ИЗ
		|	Справочник.Сотрудники.ДополнительныеРеквизиты КАК СотрудникиДополнительныеРеквизиты
		|ГДЕ
		|	СотрудникиДополнительныеРеквизиты.Ссылка = &Менеджер");
	
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		фРезультат.Добавить(Новый Структура("Свойство, Имя", Выборка.Свойство, Выборка.Имя));
	КонецЦикла;
	
	Возврат фРезультат;
КонецФункции
