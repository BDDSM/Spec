
Процедура ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборЗаписей) Экспорт
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьЗаписиРегистраРасчета(НаборЗаписей, Выборка) Экспорт
	ВидРасчетаОплатаПоТарифу = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу;
	ВидРасчетаБольничный = ПланыВидовРасчета.ОсновныеНачисления.Больничный;
	
	Пока Выборка.Следующий() Цикл
		Запись = НаборЗаписей[Выборка.НомерСтроки-1];
		
		Результат = 0;
		
		Если Выборка.ВидРасчета = ВидРасчетаОплатаПоТарифу Тогда
			Результат = Запись.Размер * Выборка.Факт;
			Запись.ОтработаноЧасов = Выборка.Факт;
		ИначеЕсли Выборка.ВидРасчета = ВидРасчетаБольничный И Выборка.ОтработаноЧасовБаза <> 0 Тогда
			Результат = Выборка.Факт * Выборка.РезультатБаза / Выборка.ОтработаноЧасовБаза;
		КонецЕсли;
		
		Запись.Результат = Результат;
	КонецЦикла;
КонецПроцедуры
