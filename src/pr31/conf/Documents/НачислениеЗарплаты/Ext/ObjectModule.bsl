
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	НаборОсновныеНачисления = Движения.ОсновныеНачисления;
	НаборДополнительныеНачисления = Движения.ДополнительныеНачисления;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	НачислениеЗарплаты.ПериодРегистрации КАК ПериодДействияНачало,
		|	ВЫБОР
		|		КОГДА ДЕНЬ(НачислениеЗарплаты.ПериодРегистрации) > ДЕНЬ(ДОБАВИТЬКДАТЕ(НачислениеЗарплаты.ПериодРегистрации, НЕДЕЛЯ, 1))
		|			ТОГДА КОНЕЦПЕРИОДА(НачислениеЗарплаты.ПериодРегистрации, МЕСЯЦ)
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(НачислениеЗарплаты.ПериодРегистрации, НЕДЕЛЯ, 1), СЕКУНДА, -1)
		|	КОНЕЦ КАК ПериодДействияКонец
		|ПОМЕСТИТЬ втПериодыДействия
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
		|ГДЕ
		|	НачислениеЗарплаты.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДЕНЬ(НачислениеЗарплаты.ПериодРегистрации) > ДЕНЬ(ДОБАВИТЬКДАТЕ(НачислениеЗарплаты.ПериодРегистрации, НЕДЕЛЯ, 1))
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплаты.ПериодРегистрации, НЕДЕЛЯ, 1), МЕСЯЦ)
		|		ИНАЧЕ НачислениеЗарплаты.ПериодРегистрации
		|	КОНЕЦ,
		|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(НачислениеЗарплаты.ПериодРегистрации, НЕДЕЛЯ, 1), СЕКУНДА, -1)
		|ИЗ
		|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
		|ГДЕ
		|	НачислениеЗарплаты.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР ОсновныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу)
		|			ТОГДА втПериодыДействия.ПериодДействияНачало
		|		ИНАЧЕ ОсновныеНачисления.ПериодДействияНачало
		|	КОНЕЦ КАК ПериодДействияНачало,
		|	ВЫБОР ОсновныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу)
		|			ТОГДА втПериодыДействия.ПериодДействияКонец
		|		ИНАЧЕ ОсновныеНачисления.ПериодДействияКонец
		|	КОНЕЦ КАК ПериодДействияКонец,
		|	ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	ВЫБОР ОсновныеНачисления.БазовыйПериодКонец
		|		КОГДА ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ОсновныеНачисления.БазовыйПериодКонец
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ОсновныеНачисления.БазовыйПериодКонец, ДЕНЬ)
		|	КОНЕЦ КАК БазовыйПериодКонец,
		|	ОсновныеНачисления.Размер КАК Размер,
		|	ОсновныеНачисления.График КАК График
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодыДействия КАК втПериодыДействия
		|		ПО (ОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу))
		|ГДЕ
		|	ОсновныеНачисления.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборОсновныеНачисления);
	НаборОсновныеНачисления.Записать(, Ложь);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	ВЫБОР ДополнительныеНачисления.БазовыйПериодКонец
		|		КОГДА ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДополнительныеНачисления.БазовыйПериодКонец
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДополнительныеНачисления.БазовыйПериодКонец, ДЕНЬ)
		|	КОНЕЦ КАК БазовыйПериодКонец
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборДополнительныеНачисления);
	НаборДополнительныеНачисления.Записать(, Ложь);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос);
	РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос);
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(ДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт,
		|	ЕСТЬNULL(БазаОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(БазаОсновныеНачисления.ОтработаноЧасовБаза, 0) КАК ОтработаноЧасовБаза
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ДанныеГрафика
		|		ПО ОсновныеНачисления.НомерСтроки = ДанныеГрафика.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаОсновныеНачисления
		|		ПО ОсновныеНачисления.НомерСтроки = БазаОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборОсновныеНачисления, Выборка);
	НаборОсновныеНачисления.Записать(, Истина);
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(КомпенсацияЗатрат.Сумма, 0) КАК СуммаЗатрат,
		|	ЕСТЬNULL(БазаОсновныеНачисления.ОтработаноЧасовБаза, 0) КАК ОтработаноЧасовБаза
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаОсновныеНачисления
		|		ПО ДополнительныеНачисления.НомерСтроки = БазаОсновныеНачисления.НомерСтроки,
		|	РегистрСведений.КомпенсацияЗатрат.СрезПоследних(&ПериодРегистрации, ) КАК КомпенсацияЗатрат
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборДополнительныеНачисления, Выборка);
	НаборДополнительныеНачисления.Записать(, Истина);
КонецПроцедуры
