
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЗадолженностьПоОтгрузке Расход
	Движения.ЗадолженностьПоОтгрузке.Записывать = Истина;
	Движения.ЗадолженностьПоОтгрузке.БлокироватьДляИзменения = Истина;
	
	Движение = Движения.ЗадолженностьПоОтгрузке.ДобавитьРасход();
	Движение.Период	 = Дата;
	Движение.Счет	 = Счет;
	Движение.Сумма	 = СуммаДокумента;
	
	Движения.Записать();
	
	// регистры СостояниеСчетов и НачислениеПеней
	Движения.СостояниеСчетов.Записывать = Истина;
	Движения.НачислениеПеней.Записывать = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗадолженностьПоОтгрузкеОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ЗадолженностьПоОтгрузке.Остатки(, Счет = &Счет) КАК ЗадолженностьПоОтгрузкеОстатки
		|ГДЕ
		|	ЗадолженностьПоОтгрузкеОстатки.СуммаОстаток > 0");
	
	Запрос.УстановитьПараметр("Счет", Счет);
	
	РезультатЗапрос = Запрос.Выполнить();
	
	Если РезультатЗапрос.Пустой() Тогда
		Движение = Движения.СостояниеСчетов.Добавить();
		Движение.Период				 = Дата;
		Движение.Счет				 = Счет;
		Движение.ДатаПолнойОтгрузки	 = Дата;
		
		Движение = Движения.НачислениеПеней.Добавить();
		Движение.Период				 = Дата;
		Движение.Счет				 = Счет;
		Движение.ДатаНачисленияПеней = Дата + Счет.ОтсрочкаПлатежа * 86400;
	КонецЕсли;
КонецПроцедуры
