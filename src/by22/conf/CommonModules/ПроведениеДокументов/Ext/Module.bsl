
Процедура ПроверкаОперативногоСписания(Объект, РежимПроведения, МВТ, Отказ) Экспорт
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(Объект.МоментВремени(), ВидГраницы.Включая));
	
	Объект.Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Объект.Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	&Период КАК Период,
		|	&Склад КАК Склад,
		|	&ВидДвижения КАК ВидДвижения
		|ИЗ
		|	втТЧ КАК втТЧ
		|ГДЕ
		|	втТЧ.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)");
	
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	
	Объект.Движения.ОстаткиНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
	Объект.Движения.Записать();
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Склад.Представление КАК Склад,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Нехватка
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ.Номенклатура
		|					ИЗ
		|						втТЧ)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На складе %1 не хватает товара %2 в количестве %3 шт.", Выборка.Склад, Выборка.Номенклатура, Выборка.Нехватка);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьПартионноеСписание(Объект, РежимПроведения, МВТ, Отказ) Экспорт
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Объект.Дата);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не задана учетная политика на %1", НачалоГода(Объект.Дата));
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	Объект.Движения.ПартииТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура
		|ИЗ
		|	втТЧ КАК втТЧ
		|ГДЕ
		|	втТЧ.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)");
	
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	РезультатЗапросаТЧ = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПартииТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапросаТЧ;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.УстановитьЗначение("Склад", Объект.Склад);
	Блокировка.Заблокировать();
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		Объект.Движения.ПартииТоваров.Записать(); // очистка движения текущего документа для оперативного перепроведения
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МоментВремени", Объект.МоментВремени());
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	ПартииТоваровОстатки.Партия КАК Партия,
		|	ПартииТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров.Остатки(
		|				&МоментВремени,
		|				Склад = &Склад
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧ.Номенклатура
		|						ИЗ
		|							втТЧ)) КАК ПартииТоваровОстатки
		|		ПО втТЧ.Номенклатура = ПартииТоваровОстатки.Номенклатура
		|ГДЕ
		|	втТЧ.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество)
		|ПО
		|	Номенклатура";
	
	Если УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
			Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			Себестоимость = ?(Списать = Выборка.КоличествоОстаток,
					Выборка.СтоимостьОстаток,
					Списать / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток);
					
			Движение = Объект.Движения.ПартииТоваров.ДобавитьРасход();
			Движение.Период			 = Объект.Дата;
			Движение.Номенклатура	 = Выборка.Номенклатура;
			Движение.Склад			 = Объект.Склад;
			Движение.Партия			 = Выборка.Партия;
			Движение.Количество		 = Списать;
			Движение.Стоимость		 = Себестоимость;
			
			ОсталосьСписать = ОсталосьСписать - Списать;
		КонецЦикла;	
	КонецЦикла;
КонецПроцедуры