
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ПартииТоваров.Очистить();
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПеремещениеТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СУММА(ПеремещениеТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.Перемещение.Товары КАК ПеремещениеТовары
		|ГДЕ
		|	ПеремещениеТовары.Ссылка = &Ссылка
		|	И ПеремещениеТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПеремещениеТовары.Номенклатура,
		|	ПеремещениеТовары.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура");
	
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Выполнить();
	
	ПроведениеДокументов.ПроверкаОперативногоСписания(ЭтотОбъект, РежимПроведения, МВТ, Отказ);
	Если Отказ Тогда Возврат; КонецЕсли;
	
	ПроведениеДокументов.СформироватьПартионноеСписание(ЭтотОбъект, РежимПроведения, МВТ, Отказ);
	Если Отказ Тогда Возврат; КонецЕсли;
	
	Движения.Записать();
	
	Движения.ПартииТоваров.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПартииТоваровОбороты.Номенклатура КАК Номенклатура,
		|	ПартииТоваровОбороты.КоличествоРасход КАК Количество,
		|	ПартииТоваровОбороты.СтоимостьРасход КАК Стоимость
		|ИЗ
		|	РегистрНакопления.ПартииТоваров.Обороты(&МоментВремени, &МоментВремени, Регистратор, ) КАК ПартииТоваровОбороты
		|ГДЕ
		|	ПартииТоваровОбороты.Регистратор = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		Движение.Период			 = Дата;
		Движение.Номенклатура	 = Выборка.Номенклатура;
		Движение.Склад			 = СкладПолучатель;
		Движение.Количество		 = Выборка.Количество;
		
		Движение = Движения.ПартииТоваров.ДобавитьПриход();
		Движение.Период			 = Дата;
		Движение.Номенклатура	 = Выборка.Номенклатура;
		Движение.Склад			 = СкладПолучатель;
		Движение.Партия			 = Ссылка;
		Движение.Количество		 = Выборка.Количество;
		Движение.Стоимость		 = Выборка.Стоимость;
	КонецЦикла;
КонецПроцедуры
