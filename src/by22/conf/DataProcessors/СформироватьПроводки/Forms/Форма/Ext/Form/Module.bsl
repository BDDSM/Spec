
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьПроводкиНаСервере(Дата);
	ПоказатьОповещениеПользователя("Готово!");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СформироватьПроводкиНаСервере(Дата)
	ВидСубконтоНоменклатура = ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыНоменклатурыУслуга = Перечисления.ВидыНоменклатуры.Услуга;
	СчетТовары = ПланыСчетов.Управленческий.Товары;
	СчетПоставщики = ПланыСчетов.Управленческий.Поставщики;
	СчетПокупатели = ПланыСчетов.Управленческий.Покупатели;
	СчетПрибылиУбытки = ПланыСчетов.Управленческий.ПрибылиУбытки;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗакупкиОбороты.Номенклатура КАК Номенклатура,
		|	ЗакупкиОбороты.СуммаОборот КАК СуммаОборот,
		|	ЗакупкиОбороты.КоличествоОборот КАК КоличествоОборот
		|ИЗ
		|	РегистрНакопления.Закупки.Обороты(&НачалоМесяца, &КонецМесяца, , ) КАК ЗакупкиОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ПродажиОбороты.СебестоимостьОборот КАК СебестоимостьОборот,
		|	ПродажиОбороты.ВыручкаОборот КАК ВыручкаОборот,
		|	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоМесяца, &КонецМесяца, , ) КАК ПродажиОбороты");
	
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));
	
	РезультатПакет = Запрос.ВыполнитьПакет();
	
	// Поступления
	ДокОбъект = Документы.Операция.СоздатьДокумент();
	ДокОбъект.Дата = КонецМесяца(Дата);
	ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	НаборЗаписей = РегистрыБухгалтерии.Проводки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	
	Выборка = РезультатПакет[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		Проводка = НаборЗаписей.Добавить();
		Проводка.Период	 = ДокОбъект.Дата;
		Проводка.СчетДт	 = СчетТовары;
		Проводка.СчетКт	 = СчетПоставщики;
		Проводка.СубконтоДт[ВидСубконтоНоменклатура] = Выборка.Номенклатура;
		Проводка.Сумма	 = Выборка.СуммаОборот;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	// Продажи
	ДокОбъект = Документы.Операция.СоздатьДокумент();
	ДокОбъект.Дата = КонецМесяца(Дата);
	ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	НаборЗаписей = РегистрыБухгалтерии.Проводки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	
	Выборка = РезультатПакет[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Проводка = НаборЗаписей.Добавить();
		Проводка.Период	 = ДокОбъект.Дата;
		Проводка.СчетДт	 = СчетПокупатели;
		Проводка.СчетКт	 = СчетПрибылиУбытки;
		Проводка.Сумма	 = Выборка.ВыручкаОборот;
		
		Если Выборка.ВидНоменклатуры <> ВидыНоменклатурыУслуга Тогда
			Проводка = НаборЗаписей.Добавить();
			Проводка.Период			 = ДокОбъект.Дата;
			Проводка.СчетДт			 = СчетПрибылиУбытки;
			Проводка.СчетКт			 = СчетТовары;
			Проводка.СубконтоКт[ВидСубконтоНоменклатура] = Выборка.Номенклатура;
			Проводка.КоличествоКт	 = Выборка.КоличествоОборот;
			Проводка.Сумма			 = Выборка.СебестоимостьОборот;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать();
КонецПроцедуры