
&НаКлиенте
Процедура Загрузить(Команда)
	Перем Имя, АдресХранилища;
	
	Если ПоместитьФайл(АдресХранилища, , Имя, Истина, ЭтаФорма.УникальныйИдентификатор) Тогда
		АдресФотографии = АдресХранилища;
		Объект.ПутьКФайлу = Имя;
		
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресФотографии) Тогда
		Если Объект.СохранятьНаДиске Тогда
			ТекущийОбъект.Фотография = Неопределено;
		Иначе
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФотографии);
			ТекущийОбъект.Фотография = Новый ХранилищеЗначения(ДвоичныеДанные);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресФотографии) И НЕ Объект.СохранятьНаДиске Тогда
		УдалитьИзВременногоХранилища(АдресФотографии);
		АдресФотографии = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Фотография");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Объект.СохранятьНаДиске Тогда
		АдресФотографии = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фотография");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.СохранятьНаДиске Тогда
		АдресХранилища = Неопределено;
		ПоместитьФайл(АдресХранилища, Объект.ПутьКФайлу, , Ложь, ЭтаФорма.УникальныйИдентификатор);
		АдресФотографии = АдресХранилища;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	ПолучитьФайл(АдресФотографии, Объект.ПутьКФайлу, Истина);
КонецПроцедуры
