
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Проводки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, Дата));
	Блокировка.Заблокировать();
	
	Движения.Проводки.Записывать = Истина;
	Движения.Проводки.Очистить();
	Движения.Проводки.Записать();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПроводкиОстатки.Валюта КАК Валюта,
		|	ПроводкиОстатки.Субконто1 КАК Контрагент,
		|	ПроводкиОстатки.Субконто2 КАК Договор,
		|	ПроводкиОстатки.ВалютнаяСуммаОстаток * ЕСТЬNULL(КурсыВалют.Курс, 1) - ПроводкиОстатки.СуммаОстаток КАК Отклонение
		|ИЗ
		|	РегистрБухгалтерии.Проводки.Остатки(&МоментВремени, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.Покупатели), , ) КАК ПроводкиОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалют
		|		ПО ПроводкиОстатки.Валюта = КурсыВалют.Валюта
		|ГДЕ
		|	(ВЫРАЗИТЬ(ПроводкиОстатки.ВалютнаяСуммаОстаток * ЕСТЬNULL(КурсыВалют.Курс, 1) КАК ЧИСЛО(15, 2))) <> ПроводкиОстатки.СуммаОстаток");
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Проводка = Движения.Проводки.Добавить();
		Проводка.Период = Дата;
		
		Если Выборка.Отклонение > 0 Тогда
			Проводка.СчетДт		 = ПланыСчетов.Управленческий.Покупатели;
			Проводка.СчетКт		 = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты]	 = Выборка.Контрагент;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]		 = Выборка.Договор;
			Проводка.ВалютаДт	 = Выборка.Валюта;
			Проводка.Сумма		 = Выборка.Отклонение;
		Иначе
			Проводка.СчетДт		 = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт		 = ПланыСчетов.Управленческий.Покупатели;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты]	 = Выборка.Контрагент;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]		 = Выборка.Договор;
			Проводка.ВалютаКт	 = Выборка.Валюта;
			Проводка.Сумма		 = -Выборка.Отклонение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
