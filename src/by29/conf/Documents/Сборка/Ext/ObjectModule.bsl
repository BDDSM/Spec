
Процедура ОбработкаПроведения(Отказ, Режим)
	ВидыСубконтоНоменклатура = ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Проводки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Состав;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ВидыСубконтоНоменклатура, "Комплектующее");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, Дата));
	Блокировка.Заблокировать();
	
	Движения.Проводки.Записывать = Истина;
	Движения.Проводки.Очистить();
	Движения.Проводки.Записать();
	
	СебестоимостьКомплекта = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СборкаСостав.Комплектующее КАК Комплектующее,
		|	СУММА(СборкаСостав.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.Сборка.Состав КАК СборкаСостав
		|ГДЕ
		|	СборкаСостав.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СборкаСостав.Комплектующее
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комплектующее
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Комплектующее КАК Комплектующее,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втТЧ.Комплектующее) КАК НаименованиеКомплектующего,
		|	втТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ПроводкиОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ПроводкиОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(
		|				&МоментВремени,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.Товары),
		|				,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						втТЧ.Комплектующее
		|					ИЗ
		|						втТЧ)) КАК ПроводкиОстатки
		|		ПО втТЧ.Комплектующее = ПроводкиОстатки.Субконто1");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Нехватка = Выборка.Количество - Выборка.КоличествоОстаток;
		Если Нехватка > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает %1 в количестве %2 шт", Выборка.НаименованиеКомплектующего, Нехватка);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда Продолжить; КонецЕсли;
		
		Себестоимость = ?(Выборка.Количество = Выборка.КоличествоОстаток,
				Выборка.СуммаОстаток,
				Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток);
		
		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт			 = ПланыСчетов.Управленческий.ОсновноеПроизводство;
		Движение.СчетКт			 = ПланыСчетов.Управленческий.Товары;
		Движение.Период			 = Дата;
		Движение.Сумма			 = Себестоимость;
		Движение.КоличествоКт	 = Выборка.Количество;
		Движение.СубконтоКт[ВидыСубконтоНоменклатура] = Выборка.Комплектующее;
		
		СебестоимостьКомплекта = СебестоимостьКомплекта + Себестоимость;
	КонецЦикла;
	
	Движение = Движения.Проводки.Добавить();
	Движение.СчетДт			 = ПланыСчетов.Управленческий.Товары;
	Движение.СчетКт			 = ПланыСчетов.Управленческий.ОсновноеПроизводство;
	Движение.Период			 = Дата;
	Движение.Сумма			 = СебестоимостьКомплекта;
	Движение.КоличествоДт	 = Количество;
	Движение.СубконтоДт[ВидыСубконтоНоменклатура] = Комплект;
КонецПроцедуры
