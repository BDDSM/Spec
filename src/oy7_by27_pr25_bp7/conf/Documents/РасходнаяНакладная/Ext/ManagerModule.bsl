
Процедура РассчитатьСебестоимость(Ссылка) Экспорт
	ДокументОбъект = Ссылка.ПолучитьОбъект();
	Если ДокументОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(ДокументОбъект.Дата).МетодСписания;
	Если НЕ ЗначениеЗаполнено(МетодСписания) Тогда
		ВызватьИсключение СтрШаблон("Не задана учетная политика на %1", НачалоГода(ДокументОбъект.Дата));
	КонецЕсли;
	
	Партии = ДокументОбъект.Движения.Партии;
	Продажи = ДокументОбъект.Движения.Продажи;
	
	Партии.Очистить();
	Продажи.Очистить();
	
	Партии.Записывать = Истина;
	Продажи.Записывать = Истина;
	
	Если НЕ ДокументОбъект.Проведен Тогда
		Партии.Записать();
		Продажи.Записать();
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)) КАК ЭтоТовар,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииОстатки.Партия КАК Партия,
		|	ПартииОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ втПартии
		|ИЗ
		|	РегистрНакопления.Партии.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					втТЧ.Номенклатура
		|				ИЗ
		|					втТЧ
		|				ГДЕ
		|					втТЧ.ЭтоТовар)) КАК ПартииОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.ЭтоТовар КАК ЭтоТовар,
		|	втТЧ.Количество КАК КоличествоПродажи,
		|	втТЧ.Сумма КАК СуммаПродажи,
		|	втПартии.Партия КАК Партия,
		|	втПартии.КоличествоОстаток КАК КоличествоОстаток,
		|	втПартии.СтоимостьОстаток КАК СтоимостьОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПартии КАК втПартии
		|		ПО втТЧ.Номенклатура = втПартии.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	втПартии.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(ЭтоТовар),
		|	МАКСИМУМ(КоличествоПродажи),
		|	МАКСИМУМ(СуммаПродажи)
		|ПО
		|	Номенклатура");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	Запрос.УстановитьПараметр("МоментВремени", ДокументОбъект.МоментВремени());
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл
		ИтогоСебестоимость = 0;
		
		Если ВыборкаИтоги.ЭтоТовар Тогда
			ОсталосьСписать = ВыборкаИтоги.КоличествоПродажи;
			
			Выборка = ВыборкаИтоги.Выбрать();
			Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
				Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
				Себестоимость = ?(Списать = Выборка.КоличествоОстаток,
						Выборка.СтоимостьОстаток,
						Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * Списать);
				ИтогоСебестоимость = ИтогоСебестоимость + Себестоимость;
				ОсталосьСписать = ОсталосьСписать - Списать;
				
				Движение = Партии.ДобавитьРасход();
				Движение.Период			 = ДокументОбъект.Дата;
				Движение.Номенклатура	 = Выборка.Номенклатура;
				Движение.Партия			 = Выборка.Партия;
				Движение.Количество		 = Списать;
				Движение.Стоимость		 = Себестоимость;
			КонецЦикла;
		КонецЕсли;
		
		Движение = Продажи.Добавить();
		Движение.Период			 = ДокументОбъект.Дата;
		Движение.Номенклатура	 = ВыборкаИтоги.Номенклатура;
		Движение.Количество		 = ВыборкаИтоги.КоличествоПродажи;
		Движение.Себестоимость	 = ИтогоСебестоимость;
		Движение.Сумма			 = ВыборкаИтоги.СуммаПродажи;
	КонецЦикла;
	
	Партии.Записать();
	Продажи.Записать();
КонецПроцедуры
