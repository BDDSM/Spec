
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ДополнительныеСвойства.Свойство("РасчитатьСебестоимость") И ДополнительныеСвойства.Проведен Тогда
		УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
		Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не задана учетная политика на %1", НачалоГода(Дата));
			Сообщение.Сообщить();
			
			Возврат;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	МоментВремени = ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.Продажи.Очистить();
	
	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)) КАК Услуга,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Услуга КАК Услуга,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма
		|ИЗ
		|	втТЧ КАК втТЧ");
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ Выборка.Услуга Тогда
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период			 = Дата;
			Движение.Номенклатура	 = Выборка.Номенклатура;
			Движение.Склад			 = Склад;
			Движение.Количество		 = Выборка.Количество;
		КонецЕсли;
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период			 = Дата;
		Движение.Номенклатура	 = Выборка.Номенклатура;
		Движение.Количество		 = Выборка.Количество;
		Движение.Сумма			 = Выборка.Сумма;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.УстановитьПараметр("Склад", 			Склад);
	Запрос.УстановитьПараметр("МоментВремени", 	МоментВремени);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОстаткиНоменклатурыОстатки.Номенклатура) КАК Номенклатура,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ.Номенклатура
		|					ИЗ
		|						втТЧ
		|					ГДЕ
		|						НЕ втТЧ.Услуга)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		НаименованиеСклада = СокрЛП(Склад);
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На складе %1 не хватает %2 в количестве %3 шт.", НаименованиеСклада, Выборка.Номенклатура, Выборка.Количество);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	ДополнительныеСвойства.Вставить("Проведен", Проведен);
КонецПроцедуры
