
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	НаборОсновныеНачисления = Движения.ОсновныеНачисления;
	НаборДополнительныеНачисления = Движения.ДополнительныеНачисления;
	НаборУдержания = Движения.Удержания;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОсновныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета
		|ПОМЕСТИТЬ втОсновныеНачисления
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|	втОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	втОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Размер
		|ИЗ
		|	втОсновныеНачисления КАК втОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&Период,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						втОсновныеНачисления.Сотрудник КАК Сотрудник
		|					ИЗ
		|						втОсновныеНачисления КАК втОсновныеНачисления)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втОсновныеНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборОсновныеНачисления);
	НаборОсновныеНачисления.Записать(, Ложь);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ДополнительныеНачисления.График КАК График,
		|	ДополнительныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	КОНЕЦПЕРИОДА(ДополнительныеНачисления.ПериодДействияКонец, ДЕНЬ) КАК ПериодДействияКонец,
		|	ВЫБОР ДополнительныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Переработка)
		|			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодНачало,
		|	ВЫБОР ДополнительныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Переработка)
		|			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ДополнительныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ, -1), МЕСЯЦ)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК БазовыйПериодКонец,
		|	ВЫБОР ДополнительныеНачисления.ВидРасчета
		|		КОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Переработка)
		|			ТОГДА ДополнительныеНачисления.Размер
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Размер
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборДополнительныеНачисления);
	НаборДополнительныеНачисления.Записать(, Ложь);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Удержания.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
		|	Удержания.Сотрудник КАК Сотрудник,
		|	Удержания.ВидРасчета КАК ВидРасчета,
		|	НАЧАЛОПЕРИОДА(Удержания.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК БазовыйПериодНачало,
		|	КОНЕЦПЕРИОДА(Удержания.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК БазовыйПериодКонец,
		|	100 КАК Размер
		|ИЗ
		|	Документ.НачислениеЗарплаты.Удержания КАК Удержания
		|ГДЕ
		|	Удержания.Ссылка = &Ссылка";
	
	Расчеты.ЗаполнитьНаборЗаписей(ТекстЗапроса, Ссылка, НаборУдержания);
	НаборУдержания.Записать(, Ложь);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос);
	РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос);
	РассчитатьУдержания(НаборУдержания, Запрос);
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(НаборОсновныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ОсновныеНачисления.Размер КАК Размер
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборОсновныеНачисления, Выборка);
	НаборОсновныеНачисления.Записать(, Истина);
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления(НаборДополнительныеНачисления, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета
		|ПОМЕСТИТЬ втДополнительныеНачисления
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	втДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(ДанныеГрафика.ЗначениеЧасыПериодДействия, 0) КАК ЧасовНорма,
		|	ЕСТЬNULL(БазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(БазаДополнительныеНачисления.РезультатБаза, 0) - ЕСТЬNULL(БазаУдержания.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(ДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ДнейФакт
		|ИЗ
		|	втДополнительныеНачисления КАК втДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ДанныеГрафика
		|		ПО втДополнительныеНачисления.НомерСтроки = ДанныеГрафика.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаОсновныеНачисления
		|		ПО втДополнительныеНачисления.НомерСтроки = БазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаДополнительныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаДополнительныеНачисления
		|		ПО втДополнительныеНачисления.НомерСтроки = БазаДополнительныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаУдержания(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК БазаУдержания
		|		ПО втДополнительныеНачисления.НомерСтроки = БазаУдержания.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборДополнительныеНачисления, Выборка);
	НаборДополнительныеНачисления.Записать(, Истина);
КонецПроцедуры

Процедура РассчитатьУдержания(НаборУдержания, Запрос)
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УдержанияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	УдержанияБазаДополнительныеНачисления.ДнейБаза КАК ДнейПрогула
		|ИЗ
		|	РегистрРасчета.Удержания.БазаДополнительныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК УдержанияБазаДополнительныеНачисления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Расчеты.РассчитатьЗаписиРегистраРасчета(НаборУдержания, Выборка);
	НаборУдержания.Записать(, Истина);
КонецПроцедуры
