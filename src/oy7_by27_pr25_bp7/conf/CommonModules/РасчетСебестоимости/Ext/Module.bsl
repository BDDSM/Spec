
Процедура РасчитатьСебестоимость() Экспорт
	ГраницаРасчетаСебестоимости = Последовательности.Расход.ПолучитьГраницу();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Расход.Регистратор КАК Регистратор,
		|	Расход.МоментВремени КАК МоментВремени
		|ИЗ
		|	Последовательность.Расход КАК Расход
		|ГДЕ
		|	Расход.МоментВремени > &Граница
		|
		|УПОРЯДОЧИТЬ ПО
		|	Расход.МоментВремени");
	
	Запрос.УстановитьПараметр("Граница", ГраницаРасчетаСебестоимости);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Последовательность.Расход");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Пока Выборка.Следующий() Цикл
				Документы.РасходнаяНакладная.РассчитатьСебестоимость(Выборка.Регистратор);
				
				ТекущаяГраница = Выборка.МоментВремени;
			КонецЦикла;
			
			Последовательности.Расход.УстановитьГраницу(ТекущаяГраница);
		Исключение
			ОтменитьТранзакцию();
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки();
			Сообщение.Сообщить();
			
			Возврат;
		КонецПопытки;
		
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Расчет себестоимости успешно завершен";
	Сообщение.Сообщить();
КонецПроцедуры
