
Процедура ОбработкаПроведения(Отказ, Режим)
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитика.МетодСписания) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не задана учетная политика на %1", НачалоГода(Дата));
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	МоментВремени = ?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	// Оперативное проведение
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура,
		|	РасходнаяНакладнаяТовары.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.Количество КАК Количество,
		|	&Период КАК Период,
		|	&Склад КАК Склад,
		|	&ВидДвижения КАК ВидДвижения
		|ИЗ
		|	втТЧ КАК втТЧ
		|ГДЕ
		|	втТЧ.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)");
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	
	РезультатЗапросаТЧ = Запрос.Выполнить();
	
	Движения.ОстаткиНоменклатуры.Загрузить(РезультатЗапросаТЧ.Выгрузить());
	Движения.Записать();
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Склад.Представление КАК Склад,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Нехватка
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ.Номенклатура
		|					ИЗ
		|						втТЧ)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На складе %1 не хватает товара %2 в количестве %3 шт.", Выборка.Склад, Выборка.Номенклатура, Выборка.Нехватка);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	// Учет партий товаров
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПартииТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапросаТЧ;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
	Блокировка.Заблокировать();
	
	Движения.ПартииТоваров.Записывать = Истина;
	Движения.ПартииТоваров.Очистить();
	
	Движения.Продажи.Записывать = Истина;
	Движения.Продажи.Очистить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	втТЧ.Номенклатура КАК Номенклатура,
		|	втТЧ.ВидНоменклатуры КАК ВидНоменклатуры,
		|	втТЧ.Количество КАК Количество,
		|	втТЧ.Сумма КАК Сумма,
		|	ПартииТоваровОстатки.Партия КАК Партия,
		|	ПартииТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ИЗ
		|	втТЧ КАК втТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров.Остатки(
		|				&МоментИтогов,
		|				Склад = &Склад
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧ.Номенклатура
		|						ИЗ
		|							втТЧ)) КАК ПартииТоваровОстатки
		|		ПО втТЧ.Номенклатура = ПартииТоваровОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(ВидНоменклатуры),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма)
		|ПО
		|	Номенклатура";
	
	Если УчетнаяПолитика.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		Движения.ПартииТоваров.Записать(); // очистка движения текущего документа для оперативного перепроведения
	КонецЕсли;
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ИтогоСебестоимость = 0;
		
		Если ВыборкаНоменклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
			ОсталосьСписать = ВыборкаНоменклатура.Количество;
			
			Выборка = ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
				Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
				Себестоимость = ?(Списать = Выборка.КоличествоОстаток,
						Выборка.СтоимостьОстаток,
						Списать / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток);
						
				// регистр ПартииТоваров
				Движение = Движения.ПартииТоваров.ДобавитьРасход();
				Движение.Период			 = Дата;
				Движение.Номенклатура	 = Выборка.Номенклатура;
				Движение.Склад			 = Склад;
				Движение.Партия			 = Выборка.Партия;
				Движение.Количество		 = Списать;
				Движение.Стоимость		 = Себестоимость;
				
				ИтогоСебестоимость = ИтогоСебестоимость + Себестоимость;
				ОсталосьСписать = ОсталосьСписать - Списать;
			КонецЦикла;
		КонецЕсли;
		
		// регистр Продажи 
		Движение = Движения.Продажи.Добавить();
		Движение.Период			 = Дата;
		Движение.Номенклатура	 = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество		 = ВыборкаНоменклатура.Количество;
		Движение.Себестоимость	 = ИтогоСебестоимость;
		Движение.Выручка		 = ВыборкаНоменклатура.Сумма;	
	КонецЦикла;
КонецПроцедуры
